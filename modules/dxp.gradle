import com.liferay.gradle.util.Validator

buildscript {
	apply from: file("build-buildscript.gradle"), to: buildscript
}

task setUpProfileDXP

String repositoryURL = "git@github.com:liferay/liferay-portal-ee.git"

if (System.getenv("JENKINS_HOME")) {
	repositoryURL = "git@github-dev.liferay.com:liferay/liferay-portal-ee.git"
}

setUpProfileDXP {
	doLast {
		exec {
			commandLine "git", "fetch", "--depth=1", "--force", "--no-tags", repositoryURL, "master-private:master-private"
			workingDir = projectDir.parentFile
		}

		exec {
			commandLine "git", "tag", "--force", "git-commit-portal-private", "master-private"
			workingDir = projectDir.parentFile
		}

		exec {
			commandLine "git", "checkout", "refs/tags/git-commit-portal-private", "--", "working.dir.properties"
			workingDir = projectDir.parentFile
		}

		exec {
			commandLine "git", "tag", "--delete", "git-commit-portal-private"
			workingDir = projectDir.parentFile
		}

		Properties workingDirProperties = GUtil.loadProperties(new File(projectDir.parentFile, "working.dir.properties"))

		String repositoryPrivatePassword = workingDirProperties.getProperty("build.repository.private.password[master-private]")
		String repositoryPrivateUsername = workingDirProperties.getProperty("build.repository.private.username[master-private]")
		String repositoryPrivateURL = workingDirProperties.getProperty("build.repository.private.url[master-private]")

		exec {
			commandLine "git", "rm", "--cached", "working.dir.properties"
			workingDir = projectDir.parentFile
		}

		File buildPropertiesFile = new File(projectDir.parentFile, "build." + System.getenv("COMPUTERNAME") + ".properties")

		String content = """\
			|##
			|## Autogenerated
			|##
			|
			|build.repository.private.password=${repositoryPrivatePassword}
			|build.repository.private.url=${repositoryPrivateURL}
			|build.repository.private.username=${repositoryPrivateUsername}""".stripMargin()

		if (buildPropertiesFile.exists() && !buildPropertiesFile.text.equals(content)) {
			buildPropertiesFile.renameTo("build." + System.getenv("COMPUTERNAME") + ".properties." + System.currentTimeMillis() + ".bak")
		}

		buildPropertiesFile.write(content)
	}

}