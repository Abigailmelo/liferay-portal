import com.liferay.gradle.util.GradleUtil
import com.liferay.gradle.util.Validator

buildscript {
	apply from: file("build-buildscript.gradle"), to: buildscript
}

task setUpProfileDXP

String repositoryURL = "git@github.com:liferay/liferay-portal-ee.git"

if (System.getenv("JENKINS_HOME")) {
	repositoryURL = "git@github-dev.liferay.com:liferay/liferay-portal-ee.git"
}

setUpProfileDXP {
	doLast {
		String branchName = GradleUtil.getProperty(project, "git.working.branch.name", (String)null)

		if (!branchName.endsWith("-private")) {
			branchName = branchName + "-private"
		}

		exec {
			commandLine "git", "fetch", "--depth=1", "--force", "--no-tags", repositoryURL, branchName + ":" + branchName
			workingDir = projectDir.parentFile
		}

		exec {
			commandLine "git", "tag", "--force", "git-commit-portal-private", branchName
			workingDir = projectDir.parentFile
		}

		exec {
			commandLine "git", "checkout", "refs/tags/git-commit-portal-private", "--", "working.dir.properties"
			workingDir = projectDir.parentFile
		}

		exec {
			commandLine "git", "tag", "--delete", "git-commit-portal-private"
			workingDir = projectDir.parentFile
		}

		Properties workingDirProperties = GUtil.loadProperties(new File(projectDir.parentFile, "working.dir.properties"))

		String repositoryPrivatePassword = workingDirProperties.getProperty("build.repository.private.password[" + branchName + "]")
		String repositoryPrivateUsername = workingDirProperties.getProperty("build.repository.private.username[" + branchName + "]")
		String repositoryPrivateURL = workingDirProperties.getProperty("build.repository.private.url[" + branchName + "]")

		exec {
			commandLine "git", "rm", "--cached", "working.dir.properties"
			workingDir = projectDir.parentFile
		}

		File buildProfileDXPPropertiesFile = new File(projectDir.parentFile, "build.profile-dxp.properties")

		String content = """\
			|##
			|## Autogenerated
			|##
			|
			|build.repository.private.password=${repositoryPrivatePassword}
			|build.repository.private.url=${repositoryPrivateURL}
			|build.repository.private.username=${repositoryPrivateUsername}""".stripMargin()

		buildProfileDXPPropertiesFile.write(content)
	}

}