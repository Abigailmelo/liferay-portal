@component-name = "portal-performance-and-fault-tolerance-ee"
definition {
	property app.server.bundles.size = "1";
	property cluster.enabled = "true";
	property portal.release = "true";
	property portal.upstream = "true";
	property remote.elasticsearch.enabled = "true";
	property test.run.environment = "EE";
	property testray.main.component.name = "Clustering";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			Page.tearDownCP();

			BlogsEntry.tearDownCP();
		}
	}

	@priority = "5"
	test AddAndDeleteBlogEntriesOnSeparateNodes {
		property test.name.skip.portal.instance = "ClusteringEE#AddAndDeleteBlogEntriesOnSeparateNodes";

		Clustering.viewClusterStatusInConsole();

		Page.assertNodePortPG(nodePort = "8080");

		ProductMenu.gotoPortlet(
			category = "Build",
			panel = "Site Administration",
			portlet = "Pages"
		);

		SitePages.addPublicPage(pageName = "Test Page1");

		Navigator.gotoPage(pageName = "Test Page1");

		Portlet.addPG(portletName = "Blogs");

		Navigator.gotoPage(pageName = "Test Page1");

		Blogs.addEntry(
			entryContent = "Blogs Entry1 Content",
			entryTitle = "Blogs Entry1 Title"
		);

		User.logoutPG(password = "test", userEmailAddress = "test@liferay.com");

		User.loginPG(
			nodePort = "9080",
			password = "test",
			userEmailAddress = "test@liferay.com"
		);

		Page.assertNodePortPG(nodePort = "9080");

		Navigator.gotoSpecificURLPage(
			pageName = "Test Page1",
			url = "http://localhost:9080"
		);

		BlogsNavigator.gotoEntryPG(
			entryContent = "Blogs Entry1 Content",
			entryTitle = "Blogs Entry1 Title"
		);

		BlogsEntry.viewEntry(
			entryContent = "Blogs Entry1 Content",
			entryTitle = "Blogs Entry1 Title"
		);

		Navigator.openNodePort(nodePort = "9080");

		ProductMenu.gotoPortlet(
			category = "Build",
			panel = "Site Administration",
			portlet = "Pages"
		);

		SitePages.addPublicPage(pageName = "Test Page2");

		Navigator.gotoSpecificURLPage(
			pageName = "Test Page2",
			url = "http://localhost:9080"
		);

		Portlet.addPG(portletName = "Blogs");

		Navigator.gotoSpecificURLPage(
			pageName = "Test Page2",
			url = "http://localhost:9080"
		);

		BlogsNavigator.gotoEntryPG(
			entryContent = "Blogs Entry1 Content",
			entryTitle = "Blogs Entry1 Title"
		);

		BlogsEntry.viewEntry(
			entryContent = "Blogs Entry1 Content",
			entryTitle = "Blogs Entry1 Title"
		);

		Navigator.gotoSpecificURLPage(
			pageName = "Test Page2",
			url = "http://localhost:9080"
		);

		Blogs.addEntry(
			entryContent = "Blogs Entry2 Content",
			entryTitle = "Blogs Entry2 Title"
		);

		Navigator.gotoSpecificURLPage(
			pageName = "Test Page2",
			url = "http://localhost:9080"
		);

		BlogsEntry.moveToRecycleBinPG(
			entryContent = "Blogs Entry2 Content",
			entryTitle = "Blogs Entry2 Title"
		);

		Navigator.openSpecificURL(url = "http://localhost:9080");

		ProductMenu.gotoPortlet(
			category = "Recycle Bin",
			panel = "Site Administration",
			portlet = "Recycle Bin"
		);

		RecycleBin.emptyCP();

		User.logoutPG(
			nodePort = "9080",
			password = "test",
			userEmailAddress = "test@liferay.com"
		);

		User.loginPG(password = "test", userEmailAddress = "test@liferay.com");

		Navigator.gotoPage(pageName = "Test Page2");

		BlogsEntry.viewNoPG(entryTitle = "Blogs Entry2 Title");

		BlogsNavigator.gotoEntryPG(
			entryContent = "Blogs Entry1 Content",
			entryTitle = "Blogs Entry1 Title"
		);

		BlogsEntry.viewEntry(
			entryContent = "Blogs Entry1 Content",
			entryTitle = "Blogs Entry1 Title"
		);

		SearchPortlets.searchEmbedded(searchTerm = "Entry2");

		SearchResultPortlet.viewNoSearchResults(searchTerm = "Entry2");
	}

	@priority = "5"
	test ShutdownAndStartupNodes {
		property test.name.skip.portal.instance = "ClusteringEE#ShutdownAndStartupNodes";

		Clustering.viewClusterStatusInConsole();

		Page.assertNodePortPG(nodePort = "8080");

		User.logoutPG(password = "test", userEmailAddress = "test@liferay.com");

		User.loginPG(
			nodePort = "9080",
			password = "test",
			userEmailAddress = "test@liferay.com"
		);

		Page.assertNodePortPG(nodePort = "9080");

		Clustering.shutdownSpecificServer(nodePort = "8080");

		Page.assertNodePortPG(nodePort = "9080");

		Clustering.startSpecificServer(
			deleteLiferayHome = "true",
			keepCachedAppServerData = "true",
			nodePort = "8080"
		);

		Page.assertNodePortPG(nodePort = "9080");

		User.logoutPG(password = "test", userEmailAddress = "test@liferay.com");

		User.loginPG(
			nodePort = "8080",
			password = "test",
			userEmailAddress = "test@liferay.com"
		);

		Page.assertNodePortPG(nodePort = "8080");

		Clustering.shutdownSpecificServer(nodePort = "9080");

		Clustering.startSpecificServer(
			deleteLiferayHome = "true",
			keepCachedAppServerData = "true",
			nodePort = "9080"
		);

		Page.assertNodePortPG(nodePort = "8080");

		User.logoutPG(password = "test", userEmailAddress = "test@liferay.com");

		User.loginPG(
			nodePort = "9080",
			password = "test",
			userEmailAddress = "test@liferay.com"
		);

		Page.assertNodePortPG(nodePort = "9080");
	}

	@priority = "4"
	test ValidateDocumentOnSeparateNodes {
		property test.name.skip.portal.instance = "ClusteringEE#ValidateDocumentOnSeparateNodes";

		Clustering.viewClusterStatusInConsole();

		Page.assertNodePortPG(nodePort = "8080");

		ProductMenu.gotoPortlet(
			category = "Build",
			panel = "Site Administration",
			portlet = "Pages"
		);

		SitePages.addPublicPage(pageName = "Documents and Media Page");

		Navigator.gotoPage(pageName = "Documents and Media Page");

		Portlet.addPG(portletName = "Documents and Media");

		DMDocument.enableActionsMenuOnPortlet();

		Navigator.gotoPage(pageName = "Documents and Media Page");

		DMDocument.addPG(
			dmDocumentFile = "Document_1.doc",
			dmDocumentTitle = "DM Document Title"
		);

		DMDocument.viewAllPG(dmDocumentTitle = "DM Document Title");

		User.logoutPG(password = "test", userEmailAddress = "test@liferay.com");

		User.loginPG(
			nodePort = "9080",
			password = "test",
			userEmailAddress = "test@liferay.com"
		);

		Page.assertNodePortPG(nodePort = "9080");

		Navigator.gotoSpecificURLPage(
			pageName = "Documents and Media Page",
			url = "http://localhost:9080"
		);

		DMDocument.viewAllPG(dmDocumentTitle = "DM Document Title");

		User.logoutPG(
			nodePort = "9080",
			password = "test",
			userEmailAddress = "test@liferay.com"
		);

		User.loginPG(password = "test", userEmailAddress = "test@liferay.com");
	}

	@priority = "4"
	test ValidateSchedulerOnSeparateNodes {
		property test.name.skip.portal.instance = "ClusteringEE#ValidateSchedulerOnSeparateNodes";

		var siteName = TestCase.getSiteName();

		Clustering.viewClusterStatusInConsole();

		var pageName = "Home";

		Page.assertNodePortPG(nodePort = "8080");

		Navigator.openURL();

		ProductMenu.gotoPortlet(
			category = "Publishing",
			panel = "Site Administration",
			portlet = "Staging"
		);

		Staging.activateStagingCP(siteName = "${siteName}");

		Navigator.openURL();

		Navigator.gotoStagedView();

		ProductMenu.gotoPortlet(
			category = "Build",
			panel = "Site Administration",
			portlet = "Pages"
		);

		SitePages.addPublicPage(pageName = "New Page Name");

		Navigator.openURL();

		Navigator.gotoStagedView();

		Navigator._gotoPage(pageName = "New Page Name");

		Staging.schedulePublishToLivePG(scheduleTitleName = "Staging Schedule Title");

		Navigator.openURL();

		AssertTextNotPresent(
			locator1 = "Home#PAGE_2_GUEST_VIEW",
			value1 = "New Page Name"
		);

		User.logoutPG(password = "test", userEmailAddress = "test@liferay.com");

		User.loginPG(
			nodePort = "9080",
			password = "test",
			userEmailAddress = "test@liferay.com"
		);

		Page.assertNodePortPG(nodePort = "9080");

		Navigator.gotoSpecificURLPage(
			pageName = "Welcome",
			url = "http://localhost:9080"
		);

		AssertTextNotPresent(
			locator1 = "Home#PAGE_2_GUEST_VIEW",
			value1 = "New Page Name"
		);

		Pause(locator1 = "120000");

		Navigator.gotoSpecificURLPage(
			pageName = "Welcome",
			url = "http://localhost:9080"
		);

		AssertTextEquals(locator1 = "Home#PAGE_2_GUEST_VIEW", value1 = "New Page Name");

		Navigator.openSpecificURL(url = "http://localhost:9080/web/guest-staging");

		ProductMenu.gotoPortlet(
			category = "Publishing",
			panel = "Site Administration",
			portlet = "Staging"
		);

		Staging.deactivateStagingCP(siteName = "${siteName}");

		User.logoutPG(
			nodePort = "9080",
			password = "test",
			userEmailAddress = "test@liferay.com"
		);

		User.loginPG(password = "test", userEmailAddress = "test@liferay.com");
	}

	test CacheReplicationbyCopy {
		property custom.properties = "cluster.link.enabled=true${line.separator}ehcache.replicator.properties.test.cache=replicatePutsViaCopy=true${line.separator}ehcache.bootstrap.cache.loader.enabled=false";
		property test.name.skip.portal.instance = "ClusteringEE#CacheReplicationbyCopy";

		Clustering.viewClusterStatusInConsole();

		Page.assertNodePortPG(nodePort = "8080");

		User.logoutPG(password = "test", userEmailAddress = "test@liferay.com");

		User.loginPG(
			nodePort = "9080",
			password = "test",
			userEmailAddress = "test@liferay.com"
		);

		Page.assertNodePortPG(nodePort = "9080");

		User.logoutPG(
			nodePort = "9080",
			password = "test",
			userEmailAddress = "test@liferay.com"
		);

		User.loginPG(password = "test", userEmailAddress = "test@liferay.com");

		for (var nodeport : list "8080,9080") {
			Clustering.getClusterNodelogFile(nodePort = "${nodeport}");
		};

		var controlAddress0 = Clustering.getClusterNodeControlAddress(nodePort = "8080");
		var transportAddress0 = Clustering.getClusterNodeTransportAddress(nodePort = "8080");
		echo("control Address0 is ${controlAddress0}");
		echo("transport Address0 is ${transportAddress0}");

		var controlAddress1 = Clustering.getClusterNodeControlAddress(nodePort = "9080");
		var transportAddress1 = Clustering.getClusterNodeTransportAddress(nodePort = "9080");
		echo("control Address1 is ${controlAddress1}");
		echo("transport Address1 is ${transportAddress1}");

		for (var nodeport : list "8080,9080") {
			Clustering.viewTextPresentOnSpecificConsole(
				nodePort = "${nodeport}",
				expectText = "Accepted view [${controlAddress0}|1] (2) [${controlAddress0}, ${controlAddress1}]"
			);

			Clustering.viewTextPresentOnSpecificConsole(
				nodePort = "${nodeport}",
				expectText = "Accepted view [${transportAddress0}|1] (2) [${transportAddress0}, ${transportAddress1}]"
			);
		};

		Clustering.deployJarOnSpecificServer(
			nodePort = "8080",
			liferayDependencyJars = "com.liferay.cluster.test.module.7.1.jar"
		);

		ProductMenu.gotoPortlet(
			category = "Configuration",
			panel = "Control Panel",
			portlet = "Server Administration"
		);

		ServerAdministration.executeScript(
			language = "Groovy",
			script = '''
import com.liferay.portal.kernel.cache.MultiVMPoolUtil;
import com.liferay.portal.kernel.cache.PortalCache;
import com.liferay.portal.kernel.util.ClassLoaderPool;
import java.lang.reflect.Constructor;

try {
	ClassLoader testModuleClassLoader = ClassLoaderPool.getClassLoader("com.liferay.cluster.test.module_2.0.0");

	Class<?> clazz = testModuleClassLoader.loadClass("com.liferay.cluster.test.module.internel.ClusterTestClass");

	Constructor<?> constructor = clazz.getConstructor(String.class);

	PortalCache testCache = MultiVMPoolUtil.getPortalCache("test.cache");

	testCache.put("test.key", constructor.newInstance("test.value"));
}
catch (Exception e) {
	out.println(e);
}
'''
		);

		AssertConsoleTextPresent(value1 = "Put test.key into test.cache");

		Clustering.deployJarOnSpecificServer(
			nodePort = "9080",
			liferayDependencyJars = "com.liferay.cluster.test.module.7.1.jar"
		);

		User.loginPG(
			nodePort = "9080",
			password = "test",
			userEmailAddress = "test@liferay.com"
		);

		ProductMenu.gotoPortlet(
			category = "Configuration",
			panel = "Control Panel",
			portlet = "Server Administration"
		);

		ServerAdministration.executeScript(
			language = "Groovy",
			script = '''
import com.liferay.portal.kernel.cache.MultiVMPoolUtil;
import com.liferay.portal.kernel.cache.PortalCache;
import java.util.List;


PortalCache<String, String> testCache = MultiVMPoolUtil.getPortalCache("test.cache");

List<String> keys = testCache.getKeys();

if (keys.isEmpty()) {
	out.println("test.cache is empty");

	return;
}

for (String key : keys) {
	out.println(key  + "=" + testCache.get(key));
}
'''
		);

		Clustering.getClusterNodelogFile(nodePort = "9080");

		for (var expectText : list "Put test.key into test.cache,test.key=test.value") {
			Clustering.viewTextPresentOnSpecificConsole(
				nodePort = "8080",
				expectText = "${expectText}"
				);

			Clustering.viewTextPresentOnSpecificConsole(
				nodePort = "8080",
				expectText = "${test.key=test.value}"
				);
		};
	}
}