@component-name = "portal-collaboration"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Content Pages";

	setUp {
		task ("Set up instance and sign in") {
			TestCase.setUpPortalInstance();

			User.firstLoginPG();
		}

		task ("Add a site via JSON") {
			JSONGroup.addGroup(groupName = "Test Site Name");
		}

		task ("Import test fragments from dependencies folder") {
			FragmentsAdmin.openFragmentsAdmin(siteURLKey = "test-site-name");

			FragmentsAdmin.importCollectionFromDependenciesFolder(collectionZipFile = "fragment-collection.zip");
		}
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			JSONGroup.deleteGroupByName(groupName = "Test Site Name");
		}
	}

	@description = "This test ensures that users can Enable and Disable Content Page Review at the instance level."
	@priority = "4"
	test EnableDisableContentPageReviewAtInstanceScope {
		property test.name.skip.portal.instance = "ContentPageReviewUsecase#EnableDisableContentPageReviewAtInstanceScope";

		SystemSettings.openSystemSettingsAdmin();

		SystemSettings.gotoConfiguration(
			configurationCategory = "Pages",
			configurationName = "Content Page Editor",
			configurationScope = "Virtual Instance Scope");

		FormFields.enableCheckbox(fieldName = "Comments Enabled");

		SystemSettings.saveConfiguration();

		SitePages.openPagesAdmin(siteURLKey = "test-site-name");

		ContentPages.addPage(pageName = "Test Content Page Name");

		PageEditor.addFragment(
			collectionName = "Collection Name",
			fragmentName = "Title");

		AssertElementPresent(
			key_tabName = "Comments",
			locator1 = "PageEditor#FRAGMENT_SIDEBAR_TAB_BUTTON");

		PageEditor.clickPublish();

		SystemSettings.openSystemSettingsAdmin();

		SystemSettings.gotoConfiguration(
			configurationCategory = "Pages",
			configurationName = "Content Page Editor",
			configurationScope = "Virtual Instance Scope");

		FormFields.disableCheckbox(fieldName = "Comments Enabled");

		SystemSettings.saveConfiguration();

		Navigator.openWithAppendToBaseURL(urlAppend = "web/test-site-name/test-content-page-name");

		ContentPages.clickPencil();

		AssertElementNotPresent(
			key_tabName = "Comments",
			locator1 = "PageEditor#FRAGMENT_SIDEBAR_TAB_BUTTON");
	}

}