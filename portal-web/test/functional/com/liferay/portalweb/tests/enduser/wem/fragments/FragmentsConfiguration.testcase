@component-name = "portal-wcm"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Fragments";

	setUp {
		task ("Set up instance and sign in") {
			TestCase.setUpPortalInstance();

			User.firstLoginPG();
		}

		task ("Add a site via JSON") {
			JSONGroup.addGroup(groupName = "Test Site Name");
		}

		task ("Add a fragment collection via JSON") {
			JSONFragment.addFragmentCollection(
				groupName = "Test Site Name",
				name = "Collection Name");
		}

		task ("Navigate to fragments admin") {
			FragmentsAdmin.openFragmentsAdmin(siteURLKey = "test-site-name");
		}

		task ("Navigate to the fragment collection") {
			FragmentsAdmin.gotoCollection(collectionName = "Collection Name");
		}
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			JSONGroup.deleteGroupByName(groupName = "Test Site Name");
		}
	}

	@priority = "4"
	test AddInvalidConfiguration {
		task ("Add a fragment section") {
			FragmentsAdmin.addSection(fragmentName = "Fragment Name");
		}

		task ("Navigate to the Configuration tab") {
			Navigator.gotoNavItem(navItem = "Configuration");
		}

		task ("Edit the configuration tab to have invalid json code") {
			FragmentsEditor.typeFileContent(fileName = "fragment_configuration_invalid.json");
		}

		task ("Assert the fragment shows where the error is, should be in line 4") {
			AssertTextEquals.assertPartialText(
				locator1 = "FragmentEditor#FRAGMENTS_EDITOR_LINE_ERROR",
				value1 = "4");
		}

		task ("Publish the fragment and assert there is an error message") {
			SelectFrameTop();

			Button.clickPublish();

			AssertTextEquals.assertPartialText(
				locator1 = "Message#ERROR",
				value1 = "ErrorPlease provide a valid configuration for the fragment.");
		}
	}

	@description = "This is a test for LPS-97184. Fragments JSON configuration can be changed within the fragment editor."
	@priority = "4"
	test AddValidConfiguration {
		task ("Add a fragment section") {
			FragmentsAdmin.addSection(fragmentName = "Fragment Name");
		}

		task ("Navigate to the Configuration tab") {
			Navigator.gotoNavItem(navItem = "Configuration");
		}

		task ("Edit the configuration tab to have json code") {
			FragmentsEditor.typeFileContent(fileName = "fragment_configuration.json");
		}

		task ("Navigate to the Code tab") {
			Navigator.gotoNavItem(navItem = "Code");
		}

		task ("Add fragment code") {
			FragmentsEditor.typeFileContent(fileName = "fragment_configuration.html");

			PortletEntry.publish();
		}
	}

}