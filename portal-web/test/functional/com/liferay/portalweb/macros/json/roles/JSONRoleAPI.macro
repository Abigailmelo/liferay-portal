definition {

	macro _addRole {
		Variables.assertDefined(parameterList = "${name},${titleMap},${descriptionMap},${type}");

		var portalURL = JSONCompany.getPortalURL();

		var curl = '''
			${portalURL}/api/jsonws/role/add-role \
				-u test@liferay.com:test \
				-d className=com.liferay.portal.kernel.model.Role \
				-d classPK=0 \
				-d name=${name} \
				-d titleMap=${titleMap} \
				-d descriptionMap=${descriptionMap} \
				-d type=${type} \
				-d subtype=
		''';

		com.liferay.poshi.runner.util.JSONCurlUtil.post("${curl}");
	}

	macro _assignRoleToUser {
		Variables.assertDefined(parameterList = "${roleId},${userId}");

		var portalURL = JSONCompany.getPortalURL();

		var curl = '''
			${portalURL}/api/jsonws/user/add-role-users \
				-u test@liferay.com:test \
				-d roleId=${roleId} \
				-d userIds=${userId}
		''';

		com.liferay.poshi.runner.util.JSONCurlUtil.post("${curl}");
	}

	macro _getRoleIdByName {
		Variables.assertDefined(parameterList = "${roleTitle}");

		var roleTitleEncoded = URLUtil.encodeUTF8("${roleTitle}");

		var roleTitleEncoded = StringUtil.replace("${roleTitleEncoded}", "+", "%20");

		var companyId = JSONCompany.getCompanyId();

		var portalURL = JSONCompany.getPortalURL();

		var curl = '''
			${portalURL}/api/jsonws/role/get-role/company-id/${companyId}/name/${roleTitleEncoded} \
				-u test@liferay.com:test
		''';
		var roleId = JSONCurlUtil.get("${curl}", "$['roleId']");

		return "${roleId}";
	}

}