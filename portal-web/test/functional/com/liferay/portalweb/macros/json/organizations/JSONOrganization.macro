definition {

	macro _addOrganization {
		Variables.assertDefined(parameterList = "${organizationName}");

		var organizationNameEncoded = URLUtil.encodeUTF8("${organizationName}");

		if (!(isSet(organizationSite))) {
			var organizationSite = "false";
		}

		var portalURL = JSONCompany.getPortalURL();

		var curl = '''
			${portalURL}/api/jsonws/organization/add-organization \
				-u test@liferay.com:test \
				-d parentOrganizationId=0 \
				-d name=${organizationNameEncoded} \
				-d type=organization \
				-d regionId=0 \
				-d countryId=0 \
				-d statusId=12017 \
				-d comments= \
				-d site=${organizationSite}
		''';

		var organizationId = JSONCurlUtil.post("${curl}", "$['organizationId']");
	}

	macro _assignUserToOrganization {
		Variables.assertDefined(parameterList = "${organizationId},${userId}");

		var portalURL = JSONCompany.getPortalURL();

		var curl = '''
			${portalURL}/api/jsonws/user/add-organization-users \
				-u test@liferay.com:test \
				-d organizationId=${organizationId} \
				-d userIds=${userId}
		''';

		var organizationName = JSONOrganization.getOrganizationName(organizationId = "${organizationId}");

		var userEmailAddress = JSONUser.getUserEmailAddress(userId = "${userId}");

		var temp = JSONCurlUtil.post("${curl}", "$");
	}

	macro _deleteOrganization {
		Variables.assertDefined(parameterList = "${organizationId}");

		var portalURL = JSONCompany.getPortalURL();

		var curl = '''
			${portalURL}/api/jsonws/organization/delete-organization \
				-u test@liferay.com:test \
				-d organizationId=${organizationId}
		''';

		var organizationName = JSONOrganization.getOrganizationName(organizationId = "${organizationId}");

		var organizationId = JSONCurlUtil.post("${curl}", "$['organizationId']");
	}

	macro addOrganization {
		Variables.assertDefined(parameterList = "${organizationName}");

		JSONOrganization._addOrganization(
			organizationName = "${organizationName}",
			organizationSite = "${organizationSite}");
	}

	macro assignUserToOrganization {
		Variables.assertDefined(parameterList = "${organizationName},${userEmailAddress}");

		var organizationId = JSONOrganization.getOrganizationIdByName(
			organizationName = "${organizationName}"
		);

		var userId = JSONUser.getUserIdByEmailAddress(userEmailAddress = "${userEmailAddress}");

		JSONOrganization._assignUserToOrganization(
			organizationId = "${organizationId}",
			userId = "${userId}");
	}

	macro deleteOrganizationByName {
		Variables.assertDefined(parameterList = "${organizationName}");

		var organizationId = JSONOrganization.getOrganizationIdByName(
			organizationName = "${organizationName}"
		);

		JSONOrganization._deleteOrganization(organizationId = "${organizationId}");
	}

	macro getOrganizationIdByName {
		Variables.assertDefined(parameterList = "${organizationName}");

		var companyId = JSONCompany.getCompanyId();

		var portalURL = JSONCompany.getPortalURL();

		var curl = '''
			${portalURL}/api/jsonws/organization/get-organizations/company-id/${companyId}/parent-organization-id/0 \
				-u test@liferay.com:test
		''';
		var organizationId = JSONCurlUtil.get("${curl}", "$.[?(@['name'] == '${organizationName}')]['organizationId']");
		var organizationId = StringUtil.replace("${organizationId}", "[&quot;", "");
		var organizationId = StringUtil.replace("${organizationId}", "&quot;]", "");

		return "${organizationId}";
	}

	macro getOrganizationName {
		Variables.assertDefined(parameterList = "${organizationId}");

		var portalURL = JSONCompany.getPortalURL();

		var curl = '''
			${portalURL}/api/jsonws/organization/get-organization/organization-id/${organizationId} \
				-u test@liferay.com:test
		''';
		var organizationName = JSONCurlUtil.get("${curl}", "$['name']");

		return "${organizationName}";
	}

}